apply from: "../../java.gradle"
apply plugin: "org.springframework.boot"
apply plugin: "docker"
apply plugin: "com.gorylenko.gradle-git-properties"
apply plugin: "jacoco"
//apply plugin: "com.github.spotbugs"

group = "com.virginvoyages"
version = '1.0'
description = "recommendation-api"

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}

dependencies {
    compile project(":components:logging-starter")
    compile project(":components:hbase-starter")
    compile project(":components:shared-code")
    
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-hateoas"
    compile "org.springframework.cloud:spring-cloud-starter-feign"
    compile "org.springframework.cloud:spring-cloud-starter-security"
    compile "org.springframework.security.oauth:spring-security-oauth2"
    compile "ch.qos.logback:logback-core:1.2.3"
    compile "net.logstash.logback:logstash-logback-encoder:$logbackVersion"
    compile "io.springfox:springfox-swagger2:$springFoxVersion"
    compile "io.springfox:springfox-swagger-ui:$springFoxVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.8.8"
    compile "joda-time:joda-time:2.9.9"
    compile "org.projectlombok:lombok:1.16.16"
    
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "io.rest-assured:rest-assured:3.0.3"
}

// The run task added by the application plugin
// is also of type JavaExec.
tasks.withType(JavaExec) {
    systemProperties System.properties
}

springBoot  {
    // This statement tells the Gradle Spring Boot plugin
    // to generate a file
    // build/resources/main/META-INF/build-info.properties
    // that is picked up by Spring Boot to display
    // via /info endpoint.
    buildInfo {
        // Generate extra build info.
        additionalProperties = [
                buildNumber: System.getenv("BUILD_NUMBER") ? System.getenv("BUILD_NUMBER") : ""
        ]
    }
}

jacocoTestReport {
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    //'**/*Data**',
                    // TODO Refactor to use naming (package or class) and regular expression
                    // Exclude Lombok generated code.
                    'com/virginvoyages/recommendations/calendar/CalendarRecommendations.class',
                    'com/virginvoyages/recommendations/calendar/CalendarRecommendation.class',
                    'com/virginvoyages/recommendations/content/ContentRecommendations.class',
                    'com/virginvoyages/recommendations/content/ContentRecommendation.class',
                    'com/virginvoyages/recommendations/herenow/HereNowRecommendations.class',
                    'com/virginvoyages/recommendations/herenow/HereNowRecommendation.class',
                    'com/virginvoyages/recommendations/marketing/MarketingRecommendations.class',
                    'com/virginvoyages/recommendations/marketing/MarketingRecommendation.class',
                    'com/virginvoyages/recommendations/shipnews/ShipNewsRecommendations.class',
                    'com/virginvoyages/recommendations/shipnews/ShipNewsRecommendation.class',
                    'com/virginvoyages/recommendations/tribes/Tribe.class',
                    'com/virginvoyages/recommendations/tribes/Tribes.class',
                    
                    // Exclude POJO
                    'com/virginvoyages/recommendations/model/KeyValue.class',
                    'com/virginvoyages/recommendations/model/Recommendation.class',
                    // Exclude Stub code
                    'com/virginvoyages/api/MockRecommendationAPI.class'
            ])
        })
    }
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
    }
}
