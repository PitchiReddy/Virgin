buildscript {
    ext {
        springBootVersion = "1.5.8.RELEASE"
        springVersion = "4.3.4.RELEASE"
        slf4jVersion = "1.7.21"
        springFoxVersion = "2.7.0"
        springCloudVersion = "Dalston.SR4"
        logbackVersion = "4.11"
        logbackCoreVersion = "1.2.3"
    }

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "se.transmode.gradle:gradle-docker:1.2"
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17"
        classpath "gradle.plugin.com.github.spotbugs:gradlePlugin:1.6.0"
        
    }
}

subprojects {
    version = '1.0'
}

allprojects {
    apply plugin: "docker"
    apply from: "${rootProject.projectDir}/java.gradle"
    
   if (project.name.contains("crossreference-api") 
   || project.name.contains("recommendation-api") || 
   project.name.contains("sailor-api") ) {
	task buildDocker(type: Docker, dependsOn: build) {
		push = true
		tagVersion = version + "." + System.getenv("BUILD_NUMBER")
		  	tag = "virginvoyages-cap-images.jfrog.io/$project.name"
		    dockerfile = file("$project.projectDir/src/main/docker/Dockerfile")
		    doFirst {
        copy {
            from "$project.projectDir/build/libs/${project.name + '-' + version}.jar"
            into stageDir
        }
    }
   }
 }
}

task dependenciesGraphDot {

    mustRunAfter "clean"

    def graphBuildDir = "build/dependenciesGraph"
    def dotFile = file "$graphBuildDir/graph.dot"

    doLast {
        delete graphBuildDir
        mkdir graphBuildDir

        dotFile << "digraph dependencies {\n"

        project.subprojects.forEach { Project subProject ->
            try {

                Configuration compileConfig = subProject.configurations["compile"]

                compileConfig
                        .dependencies
                        .grep { it.respondsTo("getDependencyProject") }
                        .forEach { dotFile << """  "$subProject.name" -> "$it.dependencyProject.name"\n""" }
            } catch (UnknownConfigurationException ignored) {
            }
        }
        dotFile << "}\n"
    }
}

task dependenciesGraph(dependsOn: "dependenciesGraphDot", type: Exec) {
    workingDir "$project.buildDir/dependenciesGraph"
    commandLine "dot", "-O", "-Tpng", "graph.dot"
}

defaultTasks "clean", "build", "buildDocker"